\part{Appendix}
\section{EBNF of input language}
\label{labelEBNF}

\begin{verbatim}


CHARACTERS

letter          = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz".
digit           = "0123456789".
digitNonZero    = "123456789".

cr              = '\r'.
lf              = '\n'.
tab             = '\t'.

//ANY = any symbol from the ASCII Character Set
char            = ANY - '"' - '\\' - '\'' - cr - lf.


TOKENS

equal           = '='.
greater         = '>'.
smaller         = '<'.
not             = '!'.
AND             = "&&".
OR              = "||".
SEMICOLON = ";".

commentStart    = "/*".
commentEnd      = "*/".

number          = digitNonZero{digit}| "0".

simpleIdentifier= letter{letter|digit}.
StringValue     = '"'char{char}'"'.

charValue       = '\''char'\''.

COMMENTS FROM "/*" TO "*/" NESTED 
COMMENTS FROM "//" TO cr lf

multilineComment= commentStart {char} commentEnd.
singlelineComment= "//" {char} lf.

IGNORE cr + lf + tab


PRODUCTIONS 


program                 = [packageDeclaration] {packageImport} classDeclaration.

packageDeclaration      = "package" identifier SEMICOLON.
packageImport           = "import" identifier SEMICOLON.

classDeclaration        = "public" "class" simpleIdentifier "{" classBlock "}".
classBlock              = { objectDeclaration | simpleDeclaration } 
                          { methodDeclaration }.

objectDeclaration       = object objectDeclarationSuffix SEMICOLON. 
simpleDeclaration       = primitiveDeclaration | primitiveArrayDeclaration | 
                          stringDeclaration | stringArrayDeclaration .

methodDeclaration       = "public" "static" ("void"|datatype) simpleIdentifier
                          "("[datatypeDescriptor{"," datatypeDescriptor}]")" 
                           "{" bodyBlock "}".

primitiveDeclaration    = primitive identifier [assignmentSuffix] SEMICOLON.
primitiveArrayDeclaration= primitiveArray identifier equal "new" primitive 
                          "[" number "]" SEMICOLON.
stringDeclaration       = "String" identifier equal "new" "String" 
                          "(" [StringValue] ")" SEMICOLON.
stringArrayDeclaration	= "String[]" identifier equal "new" "String" "["number"]" 
                          SEMICOLON.

objectDeclarationAssignmentMethodcall= identifier ( arrayDeclarationSuffix | 
                          methodCallSuffix ) SEMICOLON.

objectDeclarationSuffix = identifier equal "new" (object ( "("[expression]")" |
                          "["number"]" ) | primitive "["number"]").
assignmentSuffix        = equal expression.
methodCallSuffix        = "("[expression {"," expression}]")".
arrayDeclarationSuffix  = [arraySelector]( objectDeclarationSuffix | assignmentSuffix).
 
bodyBlock               = { whileStatement | ifStatement | returnStatement | 
                          simpleDeclaration | objectDeclarationAssignmentMethodcall | 
                          printStatement }.

whileStatement          = "while" "(" condition ")" "{" bodyBlock "}".
ifStatement             = "if" "(" condition ")" "{" bodyBlock "}" [ "else" "{" 
                          bodyBlock "}" ].
returnStatement         = "return" expression SEMICOLON.
printStatement          = "println" "(" (identifier | intValue | charValue | 
                          StringValue ) ")" SEMICOLON.

datatypeDescriptor      = datatype identifier [arraySelector].

value                   = identifier [arraySelector | (methodCallSuffix)] | 
                          intValue | charValue | booleanValue | StringValue |
                          "NULL" | not value | "(" expression ")".
factor                  = value {('*' | '/' | '%') value}.
term                    = factor {("+" | "-") factor}.
relation                = term [(equal equal | not equal | greater [equal] | 
                          smaller [equal]) term].
andExpression           = relation {(AND) relation}.
expression              = andExpression {(OR) andExpression}.

intValue                = ["-"]number.
booleanValue            = "true" | "false".

primitive               = "int" | "boolean" | "char".
primitiveArray          = "int[]" | "boolean[]" | "char[]".
object                  = simpleIdentifier.

datatype                = primitive | "String" | "String[]" | object | primitiveArray.

identifier              = simpleIdentifier {("."simpleIdentifier)}.
arraySelector           = "[" [expression] "]".


\end{verbatim}


\subsection{First Symbols(first sets)}
\label{first_sets}
\noindent
\begin{tabularx}{\linewidth}{YY}
program & ``\texttt{package}'' ``\texttt{import}'' ``\texttt{public}'' \\
packageDeclaration & ``\texttt{package}'' \\
packageImport & ``\texttt{import}'' \\
classDeclaration & ``\texttt{public}'' \\
identifier & simpleIdentifier \\
classBlock & simpleIdentifier ``\texttt{public}'' ``\texttt{String}'' ``\texttt{String[]}'' ``\texttt{int}'' ``\texttt{boolean}'' ``\texttt{char}'' ``\texttt{int[]}'' ``\texttt{boolean[]}'' ``\texttt{char[]}'' \\
objectDeclaration & simpleIdentifier \\
simpleDeclaration & ``\texttt{String}'' ``\texttt{String[]}'' ``\texttt{int}'' ``\texttt{boolean}'' ``\texttt{char}'' ``\texttt{int[]}'' ``\texttt{boolean[]}'' ``\texttt{char[]}'' \\
methodDeclaration & ``\texttt{public}'' \\
object & simpleIdentifier \\
objectDeclarationSuffix & simpleIdentifier \\
primitiveDeclaration & ``\texttt{int}'' ``\texttt{boolean}'' ``\texttt{char}'' \\
primitiveArrayDeclaration & ``\texttt{int[]}'' ``\texttt{boolean[]}'' ``\texttt{char[]}'' \\
stringDeclaration & ``\texttt{String}'' \\
stringArrayDeclaration & "String[]" \\
datatype & simpleIdentifier "String" "int" "boolean" "char" \\
datatypeDescriptor & simpleIdentifier "String" "int" "boolean" "char" \\
bodyBlock & simpleIdentifier "String" "String[]" "while" "if" "return" "int" "boolean" "char" "int[]" "boolean[]" "char[]" \\
primitive & "int" "boolean" "char" \\
assignmentSuffix & equal \\
primitiveArray & "int[]" "boolean[]" "char[]" \\
objectDeclarationAssignmentMethodcall & simpleIdentifier \\
arrayDeclarationSuffix & equal simpleIdentifier "[" \\
methodCallSuffix & "(" \\
expression & not number simpleIdentifier StringValue charValue "NULL" "-" "true" "false" \\
arraySelector & "[" \\
whileStatement & "while" \\
ifStatement & "if" \\
returnStatement & "return" \\
condition & not number simpleIdentifier StringValue charValue "NULL" "-" "true" "false" \\
value & not number simpleIdentifier StringValue charValue "NULL" "-" "true" "false" \\
intValue & number "-" \\
booleanValue & "true" "false" \\
factor & not number simpleIdentifier StringValue charValue "NULL" "-" "true" "false" \\
term & not number simpleIdentifier StringValue charValue "NULL" "-" "true" "false" \\
\end{tabularx}



\section{Work-sharing}
\label{appendix:work-sharing}
The compiler consists of several complex work packages where some fit together
and some are totally independend from each other. We tried to divide the work in
a way that packages that fit together were handled by one person (following the
principle of \emph{Too many cooks spoil the broth}). 
However the whole planning effort was handled by both team members. We always
aquired knowledge of compiler theory together, designed the work package and
determined who implemented it. Concepts like the format of the objectfile were
also developed together. \\
These were Rupert's working packages:
\begin{itemize}
  \item parser
  \item symboltable
  \item code generation
\end{itemize}
These were Matthias' working packages:
\begin{itemize}
  \item scanner
  \item symbolfile
  \item linker
  \item virtual machine
\end{itemize}

\section{Code example}
The following example calculates the fibonacci number smaller than 10. \\
The main module (\emph{MainModule.java}) imports the module \emph{Util}. The
result of a computation is printed by the method \emph{print} in \emph{Util}. 

\begin{lstlisting}[caption="MainModule.java"]
package examples;

import Util;

public class MainModule {

	public static int calcFibonacci(int a, int b) {
		
		a = a + b;
		return a;
	}
	
	public static void main() {
	
		int a = 0;
		int b = 1;
		int tmp;
		
		while (b < 10) {
			
			Util.print(b);
			
			a = calcFibonacci(a, b);
			tmp = b;
			b = a; 
			a = tmp;	
		}	
	}	
}
\end{lstlisting}

\emph{Util} itself imports the module \emph{Util2} and executes the method
\emph{printInt} which writes the result to the output channel.
\begin{lstlisting}[caption="Util.java"]
package examples;

import Util2;

public class Util {
	
	public static void print(int a) {
		Util2.printInt(a);
	}
}
\end{lstlisting}

\emph{Util2} just offers one method \emph{printInt} to write an integer value to
the output-channel. \emph{println} is a statement of our input language (see
~\ref{labelEBNF}).

\begin{lstlisting}[caption="Util2.java"]
package examples;

public class Util2 {

	public static void printInt(int a) {
		println(a);
	}
}
\end{lstlisting}
\subsection{Running the example}
To run that example we offer some scripts that may help: 
\begin{description}
\item[compile.sh] compiles the files \emph{Util2.java}, \emph{Util.java},
\emph{MainModule.java} and links the resulting objectfiles (Util2.obj, Util.obj,
MainModule.obj) to one executablefile (MainModule.bin) 
\item[run.sh] starts the virtual machine and executes the executable \emph{MainModule.bin}
\item[run\_gui.sh] starts the virtual machine with a graphical output window. 
\item[run\_debug.sh] starts the virtual machine in debug mode. This lets you step
through the instruction from the binary file and shows the content of the vm-registers. 
\end{description}
So to compile and run the example you need to type: 
\begin{lstlisting}
	./compile.sh
	./run.sh
\end{lstlisting}